module fan() {
  difference(){
   cube([30,30,10]);
   translate([3,3,0])cylinder(r=1.5,h=20,$fn=12);
   translate([30-3,30-3,0])cylinder(r=1.5,h=20,$fn=12);
   translate([30-3,3,0])cylinder(r=1.5,h=20,$fn=12);
   translate([3,30-3,0])cylinder(r=1.5,h=20,$fn=12);
   translate([15,15,0])cylinder(r=12.5,h=20,$fn=32);
  }
}


module body(front){
 h=30/2;
 module m3_hole(depth=h*3,clearance=true){
  cylinder(r=clearance?1.8:1.5,h=depth,center=true,$fn=12);
 }

 difference(){
  union() {
   cube([42,85.5,h]);  //check +2
   cube([45,42,h]); //check +3
  }

  //Chamfer (above fan)
  translate([42,42,0])rotate([0,0,-45])cube([10,10,h]); //check

  //Chamfer (top)
  translate([37,87,0])rotate([0,0,-45])cube([10,10,h]); //check

  //Stepper fixing holes
  translate([36,80,0])m3_hole();
  translate([36,49,0])m3_hole();
  translate([5,49,0])m3_hole();

  //Extruder mounting holes
  translate([29.5,37.5,0])m3_hole();
  translate([5.5,37.5,0])m3_hole();

  if(front) {
    translate([29.5,37.5,0])cylinder(r=2.9,h=3.4,$fn=12); //check
    translate([5.5,37.5,0])cylinder(r=2.9,h=3.4,$fn=12); //check
  } else {
    translate([36,80,0])cylinder(r=2.9,h=3.4,$fn=12); //check
    translate([36,49,0])cylinder(r=2.9,h=3.4,$fn=12); //check
    translate([5,49,0])cylinder(r=2.9,h=3.4,$fn=12); //check
  }

  //Stepper hole
  hull() {
   translate([20.5,65.25,0])cylinder(r=11.25,h=h*2,center=true,$fn=32);
   translate([0,54,0])cube([20,22.5,h]); //check +0.5
  }

  //Hot end cutaway
  c=0.5; //radial clearance
  translate([17.5,0.5,h])rotate([90,0,0]){
   translate([0,0,-30])cylinder(r=11.4,h=32,$fn=32);
   translate([0,0,-33])cylinder(r=8+c,h=3,$fn=32);  //check
   translate([0,0,-38.8])cylinder(r=6+c,h=5.8,$fn=32);  //check
   translate([0,0,-42.8])cylinder(r=8+c,h=4,$fn=32);   //check
   translate([0,0,-46.2])cylinder(r=4,h=3.4,$fn=32);
   translate([0,0,-58])cylinder(r=2,h=15,$fn=32);
   translate([0,0,-100])cylinder(r=1.5,h=100,$fn=32);
  }
  //Clean up cutout
  translate([14,42.7,11])cube([4,4,h]);

  //Fan cutaway
  translate([0,16.5,h])rotate([0,90,0])cylinder(r=14,h=40,$fn=32); //check +2.5
  translate([37,34,0])cylinder(r=2,h=h*2,center=true,$fn=32);
  translate([35,0,0])cube([20,32.5,h]); //check +2
  translate([35,0,0])cube([4,34,h]); //ok

  //Fan screw holes
  translate([30,28.5,3])rotate([0,90,0])m3_hole(10,clearance=false); //check
  translate([30,4.5,3])rotate([0,90,0])m3_hole(10,clearance=false); //check

 //Idler cutaway
  translate([0,54,0])cube([14,33,h]);
  translate([0,43,7])cube([14,12,h]);

  //Access cutaway
  translate([0,64.7,h])rotate([0,90,0])cylinder(r=9.8,h=50,$fn=32);
  translate([30,64.7,h])rotate([0,90,0])cylinder(r1=0,r2=18,h=15,$fn=32);

  //Nut trap
  translate([20,80,h/2]){
   rotate([0,90,0])cylinder(r=3.1,h=3,$fn=6);
   translate([0,1,0])cube([20,4.5,3.5],center=true);
   translate([1.5,5,0])cube([3,10,6],center=true);
  }
 }

  module blade(h) {
    hull(){
      cylinder(r=0.6,h=h,$fn=32);
      translate([4,0,0])cylinder(r=0.5,h=h,$fn=8);
    }
  }

  //Blades
  translate([0,1,0]) //check +2.5
  for(i=[1:6])
    translate([0.6,i*5,0])rotate([0,0,-45])blade(h=h);

  //Blade supports
  translate([0,0,h-0.5])cube([4,32,0.5]); //ok
  translate([0,0,h/2])cube([4,32,1]); //ok

  module wedge(v) {
   rotate([0,90,0])
   linear_extrude(height=v[2])
    polygon([[0,0],[0,v[1]],[v[0],0]],[[0,1,2]]);
  }

  difference() {
   translate([14,53.5,h]) wedge([12.5,7,7]);
   translate([17.5,50,h])rotate([-90,0,0])cylinder(r=2,h=10,$fn=32);
   translate([14,59,0])cube([7,10,h]);
  }

  //translate([34.5+10,2,0])rotate([0,-90,0])fan();
}

translate([10,0,0])body(front=false);
mirror([1,0,0])body(front=true);